class FireSuppressorValve is subclass of Actuator

instance variables

  finished : bool := false;

operations

public FireSuppressorValve: nat * TypeEnums`nodeType ==> FireSuppressorValve
FireSuppressorValve (id, ptype) == ( 
	ID := id;
  type := ptype;
  action := <CLOSE>;
 );

public Switch: () ==> ()
Switch() ==
 (dcl status: TypeEnums`actionType := GetAction();
  if (status = <OPEN>)
  then (World`env.RaiseAlarm();
  			World`env.SuppressSmoke());
 );

async public SetAction: TypeEnums`actionType ==> ()
SetAction(pAct) == action := pAct
pre (pAct = <OPEN>) or (pAct = <CLOSE>);

public GetAction: () ==> TypeEnums`actionType
GetAction() == return action;

public IsFinished: () ==> ()
IsFinished() == skip;

sync
  per IsFinished => finished;
  mutex(SetAction, GetAction);
thread

-- period of thread (period, jitter, delay, offset)
periodic(1000E6,0,0,0) (Switch)

end FireSuppressorValve